plugins {
    id 'com.google.protobuf' version '0.9.1'
    id 'java-library'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
    runtimeOnly 'io.grpc:grpc-netty-shaded:1.51.0'
    implementation 'io.grpc:grpc-protobuf:1.51.0'
    implementation 'io.grpc:grpc-stub:1.51.0'
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53' 
}

protobuf {
  protoc {
    artifact = 'com.google.protobuf:protoc:3.21.11'
  }
  plugins {
    grpc {
      artifact = 'io.grpc:protoc-gen-grpc-java:1.51.1'
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {}
    }
  }
}

publishing {
    publications {
        mavenKotlin(MavenPublication) {
            def appVersions = new Properties()
            def versionFile = file("../kotlin/version.properties")
            versionFile.withReader { appVersions.load(it) }
            
            groupId = "org.xmtp.proto"
            artifactId = "java"
            version = "${appVersions.getProperty('version.major')}.${appVersions.getProperty('version.minor')}.${appVersions.getProperty('version.build')}"
            from components.java
        }
    }
     repositories {
        maven {
            name = "XMTPJavaPackage"
            url = uri("https://maven.pkg.github.com/xmtp/proto")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

sourceSets {
    test {
        java {
            srcDirs = ["lib/src/test"]
        }
    }
    main {
        java {
            srcDirs = ["lib/src/main"]
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
